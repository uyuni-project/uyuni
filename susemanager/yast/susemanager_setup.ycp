{
    import "GetInstArgs";
    import "Label";
    import "Wizard";
    import "FileUtils";

    Wizard::CreateDialog();

    string customer_center = "susemanager_scc";
    if (FileUtils::Exists ("/var/lib/spacewalk/scc/default_scc")) {
       customer_center = "susemanager_scc";
    } else {
       customer_center = "susemanager_ncc";
    }

    list dialog = [
	"susemanager_reqs",
	"susemanager_ask",
	"susemanager_migration",
	"susemanager_manager",
	"susemanager_cert",
	"susemanager_db",
	customer_center,
	"susemanager_write",
	"susemanager_congratulate",
    ];

    integer id	= 0;
    any result	= `next;
    any prev	= `next;

    while ( ( id >= 0 ) && ( id < size( dialog ) ) ) {

      string module_name = dialog[id]:"";

      y2debug ("calling '%1'", module_name);

      if (id == (size (dialog) - 1)) {
          Wizard::SetNextButton (`next, Label::FinishButton ());
      }
      else if (id == (size (dialog) - 2)) {
          Wizard::SetNextButton (`next, Label::NextButton ());
      }

      result = WFM::CallFunction (module_name, [ GetInstArgs::argmap ()]);

      if (result == nil)
      {
	  result = `cancel;
      }
      if (result == `auto)
      {
	  result = prev;
      }

      if (result == `cancel || result == `abort)
      {
	  break;
      }
      else if (result == `next)
      {
	  id = id + 1;
	  prev	= `next;
      }
      else if (result == `back)
      {
	  id = id - 1;
	  prev	= `back;
      }
      else if (result == `finish)
      {
	  result = `next;
          break;
      }
    }

    if (customer_center == "susemanager_scc") {
        SCR::Execute (.target.bash,
                      sformat ("/usr/bin/touch %1; /bin/chmod 0644 %1;", "/var/lib/spacewalk/scc/migrated")
        );
    }

    Wizard::CloseDialog();
    return result;
}


}
