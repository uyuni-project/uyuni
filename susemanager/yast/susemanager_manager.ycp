{
    textdomain "susemanager";

    import "Directory";
    import "Hostname";
    import "FileUtils";
    import "GetInstArgs";
    import "IP";
    import "Popup";
    import "Stage";
    import "Wizard";

    map args = GetInstArgs::argmap ();

    string migration_file	= Directory::tmpdir + "/susemanager_migration";
    boolean migration		= FileUtils::Exists (migration_file);

    map<string,string> settings	= $[
	"MANAGER_IP"		: "",
	"MANAGER_ADMIN_EMAIL"	: "susemanager@" + Hostname::CurrentDomain (),
	"ACTIVATE_SLP"          : "n"
    ];
    if (migration)
    {
	settings["SYS_DB_PASS"]		= "novell";
    }
    else
    {
	settings["MANAGER_ENABLE_TFTP"]	= "y";
    }

    string env_file	= Directory::tmpdir + "/env_manager";
    if (FileUtils::Exists (env_file))
    {
	SCR::Execute (.target.remove, env_file);
    }
    SCR::Execute (.target.bash, sformat ("/usr/bin/touch %1; /bin/chmod 0600 %1;", env_file));

    // read existing values, if present
    foreach (string key, string value, settings, {
	string val	= getenv (key);
	if (val != nil && val != "")
	{
	    y2internal ("value for %1 present: %2", key, val);
	    settings[key]	= val;
	}
    });

    if (settings["MANAGER_IP"]:"" == "")
    {
	map out	= (map) SCR::Execute (.target.bash_output,
	    "ip -f inet -o addr show scope global | awk '{print $4}' | awk -F \/ '{print $1}'");
	settings["MANAGER_IP"]	= splitstring (out["stdout"]:"", "\n")[0]:"";
    }

    term contents = `HBox (`HSpacing (1), `VBox (

	migration ? `VBox (

	`InputField (`id ("MANAGER_IP"), `opt (`hstretch),
	    // text entry label
	    _("&IP Address of the SUSE Manager Server"), settings["MANAGER_IP"]:""),
	`InputField (`id ("SYS_DB_PASS"), `opt (`hstretch),
	    // text entry label
	    _("&Database Administrator Password"), settings["SYS_DB_PASS"]:"")
	) : `VBox (),
	`InputField (`id ("MANAGER_ADMIN_EMAIL"), `opt (`hstretch),
	    // text entry label
	    _("SUSE Manager &Administrator E-mail Address"), settings["MANAGER_ADMIN_EMAIL"]:""),
	`VSpacing (0.5),
	`Left (`CheckBox (`id ("ACTIVATE_SLP"), _("Advertise SUSE Manager via SLP"), false))
    ), `HSpacing (1));


    // help text
    string help_text	= _("<p>Fill in <b>Administrator E-mail Address</b>. It is used for notifications by SUSE Manager.</p>");

    // dialog caption
    Wizard::SetContents (_("SUSE Manager"), contents, help_text, args["enable_back"]:true, args["enable_next"]:true);

    UI::SetFocus (`id ("MANAGER_ADMIN_EMAIL"));
    if (UI::WidgetExists (`id ("MANAGER_IP")))
	UI::SetFocus (`id ("MANAGER_IP"));

    any ret	= `back;
    while (true)
    {
	ret	= UI::UserInput ();
	if (ret == `back)
	{
	    break;
	}
	if (ret == `abort && Popup::ConfirmAbort(`incomplete))
	{
	    break;
	}
	if (ret == `next)
	{
	    if (UI::WidgetExists (`id ("MANAGER_IP")) &&
		!IP::Check ((string) UI::QueryWidget (`id ("MANAGER_IP"), `Value)))
	    {
		Popup::Error (IP::Valid4 ());
		UI::SetFocus (`id ("MANAGER_IP"));
		continue;
	    }

	    if (UI::WidgetExists (`id ("MANAGER_ADMIN_EMAIL")))
	    {
		string email	= (string) UI::QueryWidget (`id ("MANAGER_ADMIN_EMAIL"), `Value);
		if (!issubstring (email, "@") || find (email, "@") == 0 || find (email, "@") == size (email) - 1)
		{
		    Popup::Error (_("The Administrator E-mail Address is not valid."));
		    UI::SetFocus (`id ("MANAGER_ADMIN_EMAIL"));
		    continue;
		}
	    }

	    // now, values are considered correct
	    foreach (string key, string value, settings, {
		string val	= value;
		if (UI::WidgetExists (`id (key)))
		{
		    if (key == "ACTIVATE_SLP")
			val = (UI::QueryWidget (`id (key), `Value) == true) ? "y" : "n";
                    else
			val = (string) UI::QueryWidget (`id (key), `Value);
		}
		setenv (key, val, true);
		// write env files
		SCR::Execute (.target.bash, sformat ("echo \"export %1='%2'\" >> %3", key, val, env_file));
	    });

	    break;
	}
    }

    return ret;
}
