{
    textdomain "susemanager";

    import "Directory";
    import "FileUtils";
    import "GetInstArgs";
    import "Hostname";
    import "Popup";
    import "Stage";
    import "Wizard";

    map args = GetInstArgs::argmap ();
    any ret	= `auto;

    string migration_file	= Directory::tmpdir + "/susemanager_migration";
    boolean migration		= FileUtils::Exists (migration_file);
    if (migration)
    {
	y2milestone ("migration was chosen, skipping this step");
	return ret;
    }

    string invalid_pw_chars	= "\"$'!+%`=#@/";
    string valid_chars = ",.:;#'+*~?][(){}/ยง&%$\"!@0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_- ";

    map<string,string> settings	= $[
	"CERT_O"	: "",
	"CERT_OU"	: "",
	"CERT_CITY"	: "",
	"CERT_STATE"	: "",
	"CERT_COUNTRY"	: "DE",
	"CERT_EMAIL"	: "",
	"CERT_PASS"	: "",
	"CERT_PASS2"	: ""
    ];

    map<string,string> labels	= $[
	// text entry label
	"CERT_O"	: _("Organization"),
	// text entry label
	"CERT_OU"	: _("Organization Unit"),
	// text entry label
	"CERT_CITY"	: _("City"),
	// text entry label
	"CERT_STATE"	: _("State"),
	// text entry label
	"CERT_COUNTRY"	: _("Country"),
	// text entry label
	"CERT_EMAIL"	: _("E-mail")
    ];

    string env_file	= Directory::tmpdir + "/env_cert";
    if (FileUtils::Exists (env_file))
    {
	SCR::Execute (.target.remove, env_file);
    }
    SCR::Execute (.target.bash, sformat ("/usr/bin/touch %1; /bin/chmod 0600 %1;", env_file));
    if (settings["CERT_EMAIL"]:"" == "")
    {
	settings["CERT_EMAIL"] = getenv ("MANAGER_ADMIN_EMAIL");
    }

    // read existing values, if present
    foreach (string key, string value, settings, {
	string val	= getenv (key);
	if (val != nil && val != "")
	{
	    y2internal ("value for %1 present: %2", key, val);
	    settings[key]	= val;
	}
    });

    term contents = `HBox (`HSpacing (1), `VBox (
	`InputField (`id ("CERT_O"), `opt (`hstretch), labels["CERT_O"]:"",
	    settings["CERT_O"]:""),
	`InputField (`id ("CERT_OU"), `opt (`hstretch), labels["CERT_OU"]:"",
	    settings["CERT_OU"]:""),
	`InputField (`id ("CERT_CITY"), `opt (`hstretch), labels["CERT_CITY"]:"",
	    settings["CERT_CITY"]:""),
	`InputField (`id ("CERT_STATE"), `opt (`hstretch), labels["CERT_STATE"]:"",
	    settings["CERT_STATE"]:""),
	`InputField (`id ("CERT_COUNTRY"), `opt (`hstretch), labels["CERT_COUNTRY"]:"",
	    settings["CERT_COUNTRY"]:""),
	// password entry label
	`Password (`id ("CERT_PASS"), `opt (`hstretch), _("SSL Pass&word"),
	    settings["CERT_PASS"]:""),
	// text entry label
	`Password (`id ("CERT_PASS2"), `opt (`hstretch), _("R&epeat Password"),
	    settings["CERT_PASS2"]:""),
	`VSpacing (0.5)
    ), `HSpacing (1));


    // help text
    string help_text	= _("<p>Here, enter data needed for the creation of an SSL certificate. The certificate is used for a number of purposes like connections to a proxy, HTTPS protocol in browsers, and more.</p>");

    // dialog caption
    Wizard::SetContents (_("Certificate Setup"), contents, help_text, args["enable_back"]:true, args["enable_next"]:true);

    UI::ChangeWidget (`id ("CERT_O"), `ValidChars, valid_chars);
    UI::ChangeWidget (`id ("CERT_OU"), `ValidChars, valid_chars);
    UI::ChangeWidget (`id ("CERT_CITY"), `ValidChars, valid_chars);
    UI::ChangeWidget (`id ("CERT_STATE"), `ValidChars, valid_chars);
    UI::ChangeWidget (`id ("CERT_COUNTRY"), `ValidChars, "ABCDEFGHIJKLMNOPQRSTUVWXYZ");
    UI::ChangeWidget (`id ("CERT_COUNTRY"), `InputMaxLength, 2);
    UI::SetFocus (`id ("CERT_O"));

    while (true)
    {
	ret	= UI::UserInput ();
	if (ret == `back)
	{
	    break;
	}
	if (ret == `abort && Popup::ConfirmAbort(`incomplete))
	{
	    break;
	}
	if (ret == `next)
	{
	    boolean missing	= false;
	    foreach (string key, [ "CERT_O", "CERT_OU", "CERT_CITY", "CERT_STATE", "CERT_COUNTRY"], {
		string val	= (string) UI::QueryWidget (`id (key), `Value);
		if (val == "")
		{
		    string label	= labels[key]:key;
		    Popup::Error (sformat (_("The value of '%1' is empty."), label));
		    UI::SetFocus (`id (key));
		    missing	= true;
		    break;
		}
	    });
	    if (missing)
		continue;

	    string pw1	= (string) UI::QueryWidget (`id ("CERT_PASS"), `Value);
	    if (pw1 == "")
	    {
		Popup::Error (_("Password is missing."));
		UI::SetFocus (`id ("CERT_PASS"));
		continue;
	    }
	    if (pw1 != UI::QueryWidget (`id ("CERT_PASS2"), `Value))
	    {
		Popup::Error (_("Passwords do not match."));
		UI::SetFocus (`id ("CERT_PASS"));
		continue;
	    }

	    if (size (pw1) < 7)
	    {
		Popup::Error (sformat (_("The password should have at least %1 characters."), 7));
		UI::SetFocus (`id ("CERT_PASS"));
		continue;
	    }
	    if (pw1 != deletechars (pw1, invalid_pw_chars))
	    {
		Popup::Error (sformat (_("The password contains invalid characters.
The invalid characters are: %1"), invalid_pw_chars));
		UI::SetFocus (`id ("CERT_PASS"));
		continue;
	    }

	    foreach (string key, string value, settings, {
		string val	= value;
		if (UI::WidgetExists (`id (key)))
		    val		= (string) UI::QueryWidget (`id (key), `Value);
		if (key == "CERT_COUNTRY")
		    val	= toupper (val);
		setenv (key, val, true);
		SCR::Execute (.target.bash, sformat ("echo \"export %1='%2'\" >> %3", key, val, env_file));
	    });

	    break;
	}
    }

    return ret;
}
